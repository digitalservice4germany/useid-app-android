import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jmailen.kotlinter'
    id 'de.mannodermaus.android-junit5'
    id 'kotlinx-serialization'
    id 'com.google.devtools.ksp'
    id 'io.sentry.android.gradle'
    id 'com.mikepenz.aboutlibraries.plugin'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "de.digitalService.useID"
        minSdk 28
        targetSdk 33
        versionCode 39
        versionName "1.3.0"

        testInstrumentationRunner "de.digitalService.useID.util.HiltTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        manifestPlaceholders = [
                sentryPublicKey: findProperty("sentryPublicKey"),
                sentryProjectId: findProperty("sentryProjectId"),
                matomoHost: findProperty("matomoHost"),
                matomoSiteId: findProperty("matomoSiteId"),
        ]
    }

    testOptions {
        packagingOptions {
            jniLibs {
                useLegacyPackaging true
            }
        }

        managedDevices {
            devices {
                pixel2api28(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 28
                    systemImageSource = "aosp"
                }
                pixel2api29(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 29
                    systemImageSource = "aosp"
                }
                pixel2api30(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 30
                    systemImageSource = "aosp"
                }
                pixel2api31(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
                pixel2api33(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
                pixel6api28(ManagedVirtualDevice) {
                    device = "Pixel 6 Pro"
                    apiLevel = 28
                    systemImageSource = "aosp"
                }
                pixel6api29(ManagedVirtualDevice) {
                    device = "Pixel 6 Pro"
                    apiLevel = 29
                    systemImageSource = "aosp"
                }
                pixel6api30(ManagedVirtualDevice) {
                    device = "Pixel 6 Pro"
                    apiLevel = 30
                    systemImageSource = "aosp"
                }
                pixel6api31(ManagedVirtualDevice) {
                    device = "Pixel 6 Pro"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
                pixel6api33(ManagedVirtualDevice) {
                    device = "Pixel 6 Pro"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
            }

            groups {
                testDevices {
                    targetDevices.add(devices.pixel2api28)
                    targetDevices.add(devices.pixel2api29)
                    targetDevices.add(devices.pixel2api30)

                    targetDevices.add(devices.pixel6api31)
                    targetDevices.add(devices.pixel6api33)

                    // Disabled for now as using many managed devices is still buggy:

                    // targetDevices.add(devices.pixel2api31)
                    // targetDevices.add(devices.pixel2api33)
                    // targetDevices.add(devices.pixel6api28)
                    // targetDevices.add(devices.pixel6api29)
                    // targetDevices.add(devices.pixel6api30)
                }
            }
        }
    }

    buildTypes {
        release {
            // Leave out optimization for now as this leads to a crash on app launch

//            minifyEnabled true
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "nfc"
    productFlavors {
        device {
            dimension = "nfc"
        }
        preview {
            dimension = "nfc"
            applicationIdSuffix = ".Preview"
            versionNameSuffix = "-preview"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xopt-in=kotlin.RequiresOptIn",
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        textReport true
    }
    namespace 'de.digitalService.useID'
    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:1.0.0"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.core:core-splashscreen:1.0.0'

    implementation 'io.github.raamcosta.compose-destinations:core:1.7.22-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.7.22-beta'

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"

    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha03'

    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-compiler:2.42"

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.8.2")

    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "io.mockk:mockk-agent-jvm:$mockk_version"
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation 'androidx.test:core:1.5.0-rc01'
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.38.1'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.38.1'
    androidTestAnnotationProcessor 'com.google.dagger:hilt-android-compiler:2.38.1'

    implementation 'com.github.jeziellago:compose-markdown:0.3.0'

    implementation 'com.google.android.exoplayer:exoplayer:2.18.1'

    implementation 'org.openecard.clients:android-lib:2.1.2'

    implementation 'xerces:xercesImpl:2.12.0'

    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.github.tony19:logback-android:2.0.0'

    implementation 'io.sentry:sentry-android:6.4.2'
    implementation 'io.sentry:sentry-compose-android:6.4.2'

    implementation "com.airbnb.android:lottie-compose:5.2.0"

    implementation "com.mikepenz:aboutlibraries-core:10.4.0"
    implementation "com.mikepenz:aboutlibraries-compose:10.4.0"

    implementation "com.github.matomo-org:matomo-sdk-android:2c0aa43"
}

kotlinter {
    ignoreFailures = false
    reporters = ['checkstyle', 'plain']
    experimentalRules = true
    disabledRules = [
            "no-wildcard-imports"
    ]
}

lintKotlinAndroidTest {
    exclude '**/*.kt'
}

lintKotlinTest {
    exclude '**/*.kt'
}

sentry {
    ignoredBuildTypes = ["debug"]
}
